#!/bin/groovy
/*
 * Licensed to the OpenAirInterface (OAI) Software Alliance under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The OpenAirInterface Software Alliance licenses this file to You under
 * the OAI Public License, Version 1.1  (the "License"); you may not use this file
 * except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.openairinterface.org/?page_id=698
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *-------------------------------------------------------------------------------
 * For more information about the OpenAirInterface (OAI) Software Alliance:
 *      contact@openairinterface.org
 */

//-------------------------------------------------------------------------------
// Abstraction function to send social media messages:
// like on Slack or Mattermost
def sendSocialMediaMessage(pipeChannel, pipeColor, pipeMessage) {
  if (params.pipelineUsesSlack != null) {
    if (params.pipelineUsesSlack) {
      slackSend channel: pipeChannel, color: pipeColor, message: pipeMessage
    }
  }
}

// Location of the CN executor node
def cn_ci_host = params.Host_CN_CI_Server

// for lock
def cn_ci_resource = params.DockerContainers

// Location of the 2nd CN executor
def new_host_flag = false
def new_host = ""
def new_host_user = ""

//-------------------------------------------------------------------------------
// Pipeline start
pipeline {
  agent {
    label cn_ci_host
  }
  options {
    disableConcurrentBuilds()
    timestamps()
    ansiColor('xterm')
    lock(cn_ci_resource)
  }

  stages {
    stage ('Verify Parameters') {
      steps {
        script {
          echo '\u2705 \u001B[32mVerify Parameters\u001B[0m'

          JOB_TIMESTAMP = sh returnStdout: true, script: 'date --utc --rfc-3339=seconds | sed -e "s#+00:00##"'
          JOB_TIMESTAMP = JOB_TIMESTAMP.trim()

          if (params.Host_CN_CI_2nd_Server_Flag != null) {
            new_host_flag = params.Host_CN_CI_2nd_Server_Flag
            if (new_host_flag) {
              new_host = params.Host_CN_CI_2nd_Server
              new_host_user = params.Host_CN_CI_2nd_Server_Login
              echo "1st Node   is ${NODE_NAME}"
              echo "2nd Node   is ${new_host}"
            } else {
              echo "Node       is ${NODE_NAME}"
            }
          } else {
            echo "Node       is ${NODE_NAME}"
          }
          echo "Git URL    is ${GIT_URL}"

          removeGitHubInfo()
          if (env.ghprbPullId != null) {
            echo "======= THIS IS A PULL REQUEST ======"
            echo "PR ID      is ${env.ghprbPullId}"
            echo "PR LINK    is ${env.ghprbPullLink}"
            echo "PR TITLE   is ${env.ghprbPullTitle}"
            sh "./ci-scripts/doGitHubPullRequestTempMerge.sh --src-branch ${env.ghprbSourceBranch} --src-commit ${env.ghprbActualCommit} --target-branch ${env.ghprbTargetBranch} --target-commit ${GIT_COMMIT}"
            sh "sleep 10"
          } else {
            echo "======= THIS IS A PUSH EVENT ======"
            echo "Git Branch is ${GIT_BRANCH}"
            echo "Git Commit is ${GIT_COMMIT}"
            // Making a hard copy of the SHA-ONE
            getCommitSha()
          }
        }
      }
    }
    stage ('Prepare Source Code') {
      steps {
        script {
          sh "git clean -x -d -f > /dev/null 2>&1"
          sh "tar -cjhf /tmp/openair-cn.tar.bz2 ."
          sh "mv /tmp/openair-cn.tar.bz2 ."
          copyTo2ndServer('openair-cn.tar.bz2', new_host_flag, new_host_user, new_host)
          sh "mkdir -p archives"
          if (new_host_flag) {
            sh "mkdir -p archives/oai-hss-cfg"
            sh "mkdir -p archives/oai-hss-cfg/freeDiameter"
            sh "mkdir -p archives/oai-mme-cfg"
            sh "mkdir -p archives/oai-mme-cfg/freeDiameter"
          }
        }
      }
    }
    stage ('Start Cassandra') {
      steps {
        script {
          myShCmd('docker run --name ci-cassandra -d -e CASSANDRA_CLUSTER_NAME="OAI HSS Cluster" -e CASSANDRA_ENDPOINT_SNITCH=GossipingPropertyFileSnitch cassandra:2.1', new_host_flag, new_host_user, new_host)
          sh "sleep 5"
          myShCmd('docker cp src/hss_rel14/db/oai_db.cql ci-cassandra:/home', new_host_flag, new_host_user, new_host)
          myShCmd('docker exec -it ci-cassandra /bin/bash -c "nodetool status" | tee archives/cassandra_status.log', new_host_flag, new_host_user, new_host)
          sh "sleep 5"
          Cassandra_IP = myShRetCmd('docker inspect --format="{{range .NetworkSettings.Networks}}{{.IPAddress}}{{end}}" ci-cassandra', new_host_flag, new_host_user, new_host)
          myShCmd('docker exec -it ci-cassandra /bin/bash -c "cqlsh --file /home/oai_db.cql ' + Cassandra_IP + '"', new_host_flag, new_host_user, new_host)
        }
      }
      post {
        always {
          script {
            copyFrom2ndServer('archives/cassandra_status.log', 'archives', new_host_flag, new_host_user, new_host)
          }
        }
        success {
          sh "echo 'CASSANDRA START: OK' >> archives/cassandra_status.log"
        }
        unsuccessful {
          sh "echo 'CASSANDRA START: KO' >> archives/cassandra_status.log"
        }
      }
    }
    stage ('Start OAI HSS Container') {
      steps {
        script {
          myShCmd('docker run --name ci-oai-hss -d ubuntu:bionic /bin/bash -c "sleep infinity"', new_host_flag, new_host_user, new_host)
          myShCmd('docker cp ./openair-cn.tar.bz2 ci-oai-hss:/home', new_host_flag, new_host_user, new_host)
          myShCmd('docker exec -it ci-oai-hss /bin/bash -c "cd /home && tar -xjf openair-cn.tar.bz2"', new_host_flag, new_host_user, new_host)
          myShCmd('docker exec -it ci-oai-hss /bin/bash -c "rm -f /home/openair-cn.tar.bz2"', new_host_flag, new_host_user, new_host)
          myShCmd('docker cp ~/.gitconfig ci-oai-hss:/root', new_host_flag, new_host_user, new_host)
        }
      }
      post {
        success {
          sh "echo 'OAI-HSS START: OK' > archives/hss-docker-start.log"
        }
        unsuccessful {
          sh "echo 'OAI-HSS START: KO' > archives/hss-docker-start.log"
        }
      }
    }
    stage ('Start OAI MME Container') {
      steps {
        script {
          myShCmd('docker run --name ci-oai-mme -d ubuntu:bionic /bin/bash -c "sleep infinity"', new_host_flag, new_host_user, new_host)
          myShCmd('docker cp ./openair-cn.tar.bz2 ci-oai-mme:/home', new_host_flag, new_host_user, new_host)
          myShCmd('docker exec -it ci-oai-mme /bin/bash -c "cd /home && tar -xjf openair-cn.tar.bz2"', new_host_flag, new_host_user, new_host)
          myShCmd('docker exec -it ci-oai-mme /bin/bash -c "rm -f /home/openair-cn.tar.bz2"', new_host_flag, new_host_user, new_host)
          myShCmd('docker cp ~/.gitconfig ci-oai-mme:/root', new_host_flag, new_host_user, new_host)
        }
      }
      post {
        success {
          sh "echo 'OAI-MME START: OK' > archives/mme-docker-start.log"
        }
        unsuccessful {
          sh "echo 'OAI-MME START: KO' > archives/mme-docker-start.log"
        }
      }
    }
    stage('Install libs & sw') {
      parallel {
        stage ('HSS SW') {
          steps {
            script {
              myShCmd('docker exec -it ci-oai-hss /bin/bash -c "apt-get update && apt-get upgrade --yes" > archives/hss_install.log', new_host_flag, new_host_user, new_host)
              myShCmd('docker exec -it ci-oai-hss /bin/bash -c "apt-get install --yes git psmisc net-tools" >> archives/hss_install.log', new_host_flag, new_host_user, new_host)
              myShCmd('docker exec -it ci-oai-hss /bin/bash -c "cd /home/scripts && ./build_hss_rel14 --check-installed-software --force" >> archives/hss_install.log', new_host_flag, new_host_user, new_host)
            }
          }
          post {
            always {
              script {
                copyFrom2ndServer('archives/hss_install.log', 'archives', new_host_flag, new_host_user, new_host)
              }
            }
            success {
              sh "echo 'OAI-HSS SW INSTALL: OK' >> archives/hss_install.log"
            }
            unsuccessful {
              sh "echo 'OAI-HSS SW INSTALL: KO' >> archives/hss_install.log"
            }
          }
        }
        stage ('MME SW') {
          steps {
            script {
              myShCmd('docker exec -it ci-oai-mme /bin/bash -c "apt-get update && apt-get upgrade --yes" > archives/mme_install.log', new_host_flag, new_host_user, new_host)
              myShCmd('docker exec -it ci-oai-mme /bin/bash -c "apt-get install --yes git psmisc net-tools bc" >> archives/mme_install.log', new_host_flag, new_host_user, new_host)
              myShCmd('docker exec -it ci-oai-mme /bin/bash -c "cd /home/scripts && ./build_mme --check-installed-software --force" >> archives/mme_install.log', new_host_flag, new_host_user, new_host)
            }
          }
          post {
            always {
              script {
                copyFrom2ndServer('archives/mme_install.log', 'archives', new_host_flag, new_host_user, new_host)
              }
            }
            success {
              sh "echo 'OAI-MME SW INSTALL: OK' >> archives/mme_install.log"
            }
            unsuccessful {
              sh "echo 'OAI-MME SW INSTALL: KO' >> archives/mme_install.log"
            }
          }
        }
        // Running CPPCHECK in parallel to gain time
        stage ('Static Code Analysis') {
          steps {
            script {
              // Running on xenial to have 1.72 version of cppcheck
              myShCmd('docker run --name ci-cn-cppcheck -d ubuntu:xenial /bin/bash -c "sleep infinity"', new_host_flag, new_host_user, new_host)
              myShCmd('docker exec -it ci-cn-cppcheck /bin/bash -c "apt-get update && apt-get upgrade --yes" > archives/cppcheck_install.log', new_host_flag, new_host_user, new_host)
              myShCmd('docker exec -it ci-cn-cppcheck /bin/bash -c "apt-get install --yes cppcheck bzip2" >> archives/cppcheck_install.log', new_host_flag, new_host_user, new_host)

              myShCmd('docker cp ./openair-cn.tar.bz2 ci-cn-cppcheck:/home', new_host_flag, new_host_user, new_host)
              myShCmd('docker exec -it ci-cn-cppcheck /bin/bash -c "cd /home && tar -xjf openair-cn.tar.bz2"', new_host_flag, new_host_user, new_host)
              myShCmd('docker exec -it ci-cn-cppcheck /bin/bash -c "rm -f /home/openair-cn.tar.bz2"', new_host_flag, new_host_user, new_host)

              myShCmd('docker exec -it ci-cn-cppcheck /bin/bash -c "cd /home && cppcheck --enable=warning --force --xml --xml-version=2 -i src/oai_sgw -i src/sgw -i src/fluid/fluidmsg/test -i src/gtpv2-c/nwgtpv2c-0.11/test-app -i src/nas/test -i src/test -i src/oai_hss/tests -i src/utils/bstr/bstest.c -i src/utils/bstr/test.cpp -i src/utils/bstr/testaux.c --suppressions-list=ci-scripts/cppcheck_suppressions.list src 2> cppcheck.xml 1> cppcheck_build.log"', new_host_flag, new_host_user, new_host)
            }
          }
          post {
            always {
              script {
                myShCmd('docker cp ci-cn-cppcheck:/home/cppcheck.xml archives', new_host_flag, new_host_user, new_host)
                myShCmd('docker cp ci-cn-cppcheck:/home/cppcheck_build.log archives', new_host_flag, new_host_user, new_host)
                copyFrom2ndServer('archives/cppcheck*.*', 'archives', new_host_flag, new_host_user, new_host)
                // no need to keep the cppcheck container
                myShCmd('docker rm -f ci-cn-cppcheck', new_host_flag, new_host_user, new_host)
              }
            }
            success {
              sh "echo 'CPPCHECK: OK' >> archives/cppcheck_install.log"
            }
            unsuccessful {
              sh "echo 'CPPCHECK: KO' >> archives/cppcheck_install.log"
            }
          }
        }
      }
    }
    stage('Build Core Network Functions') {
      parallel {
        stage ('HSS Build') {
          steps {
            script {
              myShCmd('docker exec -it ci-oai-hss /bin/bash -c "cd /home/scripts && ./build_hss_rel14 --clean --build-type Release" > archives/hss_build.log', new_host_flag, new_host_user, new_host)
            }
          }
          post {
            always {
              script {
                copyFrom2ndServer('archives/hss_build.log', 'archives', new_host_flag, new_host_user, new_host)
              }
            }
            success {
              sh "echo 'OAI-HSS BUILD: OK' >> archives/hss_build.log"
            }
            unsuccessful {
              sh "echo 'OAI-HSS BUILD: KO' >> archives/hss_build.log"
            }
          }
        }
        stage ('MME Build') {
          steps {
            script {
              myShCmd('docker exec -it ci-oai-mme /bin/bash -c "cd /home/scripts && ./build_mme --clean --build-type Release" > archives/mme_build.log', new_host_flag, new_host_user, new_host)
              myShCmd('docker cp ci-oai-mme:/home/build/log/mme.txt archives/mme_compile.log ', new_host_flag, new_host_user, new_host)
            }
          }
          post {
            always {
              script {
                copyFrom2ndServer('archives/mme_build.log', 'archives', new_host_flag, new_host_user, new_host)
                copyFrom2ndServer('archives/mme_compile.log', 'archives', new_host_flag, new_host_user, new_host)
              }
            }
            success {
              sh "echo 'OAI-MME BUILD: OK' >> archives/mme_build.log"
            }
            unsuccessful {
              sh "echo 'OAI-MME BUILD: KO' >> archives/mme_build.log"
            }
          }
        }
      }
    }
    stage('Config Core Network Functions') {
      parallel {
        stage ('HSS Config') {
          steps {
            script {
              Cassandra_IP = myShRetCmd('docker inspect --format="{{range .NetworkSettings.Networks}}{{.IPAddress}}{{end}}" ci-cassandra', new_host_flag, new_host_user, new_host)
              OAI_HSS_IP = myShRetCmd('docker inspect --format="{{range .NetworkSettings.Networks}}{{.IPAddress}}{{end}}" ci-oai-hss', new_host_flag, new_host_user, new_host)
              sh "python3 ci-scripts/generateConfigFiles.py --kind=HSS --cassandra=${Cassandra_IP} --hss_s6a=${OAI_HSS_IP}"
              copyTo2ndServer('hss-cfg.sh', new_host_flag, new_host_user, new_host)
              myShCmd('docker cp ./hss-cfg.sh ci-oai-hss:/home/scripts', new_host_flag, new_host_user, new_host)
              myShCmd('docker exec -it ci-oai-hss /bin/bash -c "openssl rand -out /root/.rnd 128" > archives/hss_config.log', new_host_flag, new_host_user, new_host)
              myShCmd('docker exec -it ci-oai-hss /bin/bash -c "cd /home/scripts && chmod 777 hss-cfg.sh && ./hss-cfg.sh" >> archives/hss_config.log', new_host_flag, new_host_user, new_host)
            }
          }
          post {
            always {
              script {
                myShCmd('docker cp ci-oai-hss:/usr/local/etc/oai/. archives/oai-hss-cfg', new_host_flag, new_host_user, new_host)
                copyFrom2ndServer('archives/hss_config.log', 'archives', new_host_flag, new_host_user, new_host)
                copyFrom2ndServer('archives/oai-hss-cfg/*.*', 'archives/oai-hss-cfg', new_host_flag, new_host_user, new_host)
                copyFrom2ndServer('archives/oai-hss-cfg/freeDiameter/*.*', 'archives/oai-hss-cfg/freeDiameter', new_host_flag, new_host_user, new_host)
              }
            }
            success {
              sh "echo 'OAI-HSS CONFIG: OK' >> archives/hss_config.log"
            }
            unsuccessful {
              sh "echo 'OAI-HSS CONFIG: KO' >> archives/hss_config.log"
            }
          }
        }
        stage ('MME Config') {
          steps {
            script {
              OAI_HSS_IP = myShRetCmd('docker inspect --format="{{range .NetworkSettings.Networks}}{{.IPAddress}}{{end}}" ci-oai-hss', new_host_flag, new_host_user, new_host)
              OAI_MME_IP = myShRetCmd('docker inspect --format="{{range .NetworkSettings.Networks}}{{.IPAddress}}{{end}}" ci-oai-mme', new_host_flag, new_host_user, new_host)
              sh "python3 ci-scripts/generateConfigFiles.py --kind=MME --mme_s6a=${OAI_MME_IP} --hss_s6a=${OAI_HSS_IP}"
              copyTo2ndServer('mme-cfg.sh', new_host_flag, new_host_user, new_host)
              myShCmd('docker cp ./mme-cfg.sh ci-oai-mme:/home/scripts', new_host_flag, new_host_user, new_host)
              myShCmd('docker exec -it ci-oai-mme /bin/bash -c "openssl rand -out /root/.rnd 128" > archives/mme_config.log', new_host_flag, new_host_user, new_host)
              myShCmd('docker exec -it ci-oai-mme /bin/bash -c "cd /home/scripts && chmod 777 mme-cfg.sh && ./mme-cfg.sh" >> archives/mme_config.log', new_host_flag, new_host_user, new_host)
            }
          }
          post {
            always {
              script {
                myShCmd('docker cp ci-oai-mme:/usr/local/etc/oai/. archives/oai-mme-cfg', new_host_flag, new_host_user, new_host)
                copyFrom2ndServer('archives/mme_config.log', 'archives', new_host_flag, new_host_user, new_host)
                copyFrom2ndServer('archives/oai-mme-cfg/*.*', 'archives/oai-mme-cfg', new_host_flag, new_host_user, new_host)
                copyFrom2ndServer('archives/oai-mme-cfg/freeDiameter/*.*', 'archives/oai-mme-cfg/freeDiameter', new_host_flag, new_host_user, new_host)
              }
            }
            success {
              sh "echo 'OAI-MME CONFIG: OK' >> archives/mme_config.log"
            }
            unsuccessful {
              sh "echo 'OAI-MME CONFIG: KO' >> archives/mme_config.log"
            }
          }
        }
      }
    }
    stage ('Start-Check-Stop OAI cNFs') {
      steps {
        script {
          myShCmd('docker exec -d -w /home/scripts ci-oai-hss /bin/bash -c "nohup oai_hss -j /usr/local/etc/oai/hss_rel14.json --reloadkey true > hss_check_run.log 2>&1"', new_host_flag, new_host_user, new_host)
          sh "sleep 2"
          myShCmd('docker exec -d -w /home/scripts ci-oai-mme /bin/bash -c "nohup ./run_mme --config-file /usr/local/etc/oai/mme.conf --set-virt-if > mme_check_run.log 2>&1"', new_host_flag, new_host_user, new_host)
          sh "sleep 20"
          myShCmd('docker exec -it ci-oai-hss /bin/bash -c "killall oai_hss"', new_host_flag, new_host_user, new_host)
          myShCmd('docker exec -it ci-oai-mme /bin/bash -c "killall mme"', new_host_flag, new_host_user, new_host)
        }
      }
      post {
        always {
          myShCmd('docker cp ci-oai-hss:/home/scripts/hss_check_run.log archives', new_host_flag, new_host_user, new_host)
          myShCmd('docker cp ci-oai-mme:/home/scripts/mme_check_run.log archives', new_host_flag, new_host_user, new_host)
          copyFrom2ndServer('archives/*_check_run.log', 'archives', new_host_flag, new_host_user, new_host)
          // add a check python script
        }
        success {
          sh "echo 'OAI-HSS CHECK START/STOP: OK' > archives/hss_cnf_check_start.log"
          sh "echo 'OAI-MME CHECK START/STOP: OK' > archives/mme_cnf_check_start.log"
        }
        unsuccessful {
          sh "echo 'OAI-HSS CHECK START/STOP: KO' > archives/hss_cnf_check_start.log"
          sh "echo 'OAI-MME CHECK START/STOP: KO' > archives/mme_cnf_check_start.log"
        }
      }
    }
  }
  post {
    success {
      script {
        def message = "Build job #" + BUILD_ID + " completed"
        updateGithubCommitStatus(currentBuild, message)
        def socialMessage = "OAI " + JOB_NAME + " build (" + BUILD_ID + "): passed (" + BUILD_URL + ")"
        sendSocialMediaMessage('ci-epc', 'good', socialMessage)
      }
    }
    failure {
      script {
        def message = "Build job #" + BUILD_ID + " : One step of job failed"
        updateGithubCommitStatus(currentBuild, message)
        def socialMessage = "OAI " + JOB_NAME + " build (" + BUILD_ID + "): failed (" + BUILD_URL + ")"
        sendSocialMediaMessage('ci-epc', 'danger', socialMessage)
      }
    }
    unstable {
      script {
        def message = "Build job #" + BUILD_ID + " : One step of job is unstable"
        updateGithubCommitStatus(currentBuild, message)
        def socialMessage = "OAI " + JOB_NAME + " build (" + BUILD_ID + "): unstable (" + BUILD_URL + ")"
        sendSocialMediaMessage('ci-epc', 'danger', socialMessage)
      }
    }
    aborted {
      script {
        def message = "Build job #" + BUILD_ID + " aborted: pipeline script issue?"
        updateGithubCommitStatus(currentBuild, message)
        def socialMessage = "OAI " + JOB_NAME + " build (" + BUILD_ID + "): aborted (" + BUILD_URL + ")"
        sendSocialMediaMessage('ci-epc', 'danger', socialMessage)
      }
    }
    cleanup {
      script {
        // Killing all containers
        myShCmd('docker rm -f ci-cassandra ci-oai-hss ci-oai-mme', new_host_flag, new_host_user, new_host)
        // Zipping all archived log files
        sh "zip -r -qq docker_logs.zip archives"
        if (fileExists('docker_logs.zip')) {
          archiveArtifacts artifacts: 'docker_logs.zip'
        }
        // Generating the HTML report
        if (env.ghprbPullId != null) {
          sh "python3 ci-scripts/generateHtmlReport.py --job_name=${JOB_NAME} --job_id=${BUILD_ID} --job_url=${BUILD_URL} --git_url=${GIT_URL} --git_src_branch=${env.ghprbSourceBranch} --git_src_commit=${env.ghprbActualCommit} --git_pull_request=True --git_target_branch=${env.ghprbTargetBranch} --git_target_commit=${GIT_COMMIT}"
          sh "sed -i -e 's#TEMPLATE_PULL_REQUEST_LINK#${env.ghprbPullLink}#g' test_results_oai_cn.html"
          sh "sed -i -e 's#TEMPLATE_PULL_REQUEST_TEMPLATE#${env.ghprbPullTitle}#' test_results_oai_cn.html"
        } else {
          sh "python3 ci-scripts/generateHtmlReport.py --job_name=${JOB_NAME} --job_id=${BUILD_ID} --job_url=${BUILD_URL} --git_url=${GIT_URL} --git_src_branch=${GIT_BRANCH} --git_src_commit=${GIT_COMMIT}"
        }
		sh "sed -i -e 's#TEMPLATE_TIME#${JOB_TIMESTAMP}#' test_results_oai_cn.html"
        if (fileExists('test_results_oai_cn.html')) {
          archiveArtifacts artifacts: 'test_results_oai_cn.html'
        }
        // Sending an email to the last committer. Maybe not the Pull Request's author.
        def emailTo = getCommitterEmail()
        emailext attachmentsPattern: '*results*.html',
          body: '''Hi,
Here are attached HTML report files for $PROJECT_NAME - Build # $BUILD_NUMBER - $BUILD_STATUS!

Regards,
OAI CI Team''',
          replyTo: 'no-reply@openairinterface.org',
          subject: '$PROJECT_NAME - Build # $BUILD_NUMBER - $BUILD_STATUS!',
          to: emailTo
      }
    }
  }
}

// Removing all info -- do not know if git clean command removes them
def removeGitHubInfo() {
  if (fileExists('.git/remote-url')) {
    sh "rm -f .git/remote-url"
  }
  if (fileExists('.git/current-commit')) {
    sh "rm -f .git/current-commit"
  }
  if (fileExists('.git/commit-email')) {
    sh "rm -f .git/commit-email"
  }
}

// GitHub retrieve email of last committer
def getCommitterEmail() {
  if (!fileExists('.git/commit-email')) {
    sh "git log -1 --pretty=format:\"%ce\" > .git/commit-email"
    // Workaround for issue when merge to develop from the Pull Request GitHub webpage
    // Click on merge button creates a commit with an email address in noreply@github.com
    sh "if [ `egrep -c \"noreply@github.com\" .git/commit-email` -eq 1 ]; then echo \"raphael.defosseux@openairinterface.org\" > .git/commit-email; fi"
  }
  return readFile(".git/commit-email").trim()
}

// Github commit update functions
def getRepoURL() {
  if (!fileExists('.git/remote-url')) {
    sh "git config --get remote.origin.url > .git/remote-url"
  }
  return readFile(".git/remote-url").trim()
}

def getCommitSha() {
  if (!fileExists('.git/current-commit')) {
    sh "git rev-parse HEAD > .git/current-commit"
  }
  return readFile(".git/current-commit").trim()
}

def updateGithubCommitStatus(build, jobMessage) {
  // workaround https://issues.jenkins-ci.org/browse/JENKINS-38674
  repoUrl = getRepoURL()
  commitSha = getCommitSha()

  step([
    $class: 'GitHubCommitStatusSetter',
    reposSource: [$class: "ManuallyEnteredRepositorySource", url: repoUrl],
    commitShaSource: [$class: "ManuallyEnteredShaSource", sha: commitSha],
    errorHandlers: [[$class: 'ShallowAnyErrorHandler']],
    statusResultSource: [
      $class: 'ConditionalStatusResultSource',
      results: [
        [$class: 'AnyBuildResult', state: build.currentResult, message: jobMessage]
      ]
    ]
  ])
}

def copyTo2ndServer(filename, flag, user, host) {
  if (flag) {
    if ("openair-cn.tar.bz2".equals(filename)) {
      sh "ssh ${user}@${host} 'rm -rf /tmp/CI-CN'"
      sh "ssh ${user}@${host} 'mkdir -p /tmp/CI-CN'"
    }
    sh "scp ${filename} ${user}@${host}:/tmp/CI-CN"
    if ("openair-cn.tar.bz2".equals(filename)) {
      sh "ssh ${user}@${host} 'cd /tmp/CI-CN && tar -xjf ${filename}'"
      sh "ssh ${user}@${host} 'mkdir -p /tmp/CI-CN/archives'"
      sh "ssh ${user}@${host} 'mkdir -p /tmp/CI-CN/archives/oai-hss-cfg'"
      sh "ssh ${user}@${host} 'mkdir -p /tmp/CI-CN/archives/oai-mme-cfg'"
    }
  }
}

def copyFrom2ndServer(filename, target, flag, user, host) {
  if (flag) {
    sh "scp ${user}@${host}:/tmp/CI-CN/${filename} ${target}"
  }
}

def myShCmd(cmd, flag, user, host) {
  if (flag) {
    sh "ssh -t -t ${user}@${host} 'cd /tmp/CI-CN && ${cmd}'"
  } else {
    sh "${cmd}"
  }
}

def myShRetCmd(cmd, flag, user, host) {
  if (flag) {
    ret = sh returnStdout: true, script: "ssh -t -t ${user}@${host} 'cd /tmp/CI-CN && ${cmd}'"
  } else {
    ret = sh returnStdout: true, script: "${cmd}"
  }
  ret = ret.trim()
  return ret
}
